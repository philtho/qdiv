## rules to generate the usual suspects...
## - alpha diversity boxplots
## - beta pcoa, unifrac, wunifrac

diversity_out = diversity_in + "/diversity"
alpha_measures = [ "faith_pd", "evenness", "shannon", "observed_features" ]
beta_measures = [ "bray_curtis", "unweighted_unifrac", "weighted_unifrac" ]

rule diversity:
	input:
		alpha = expand(
			diversity_out + "/{measure}_significance.qzv",
			measure = alpha_measures
		),
		beta = expand(
			diversity_out + "/{measure}-by-{col}.qzv",
			measure = beta_measures,
			col = beta_groups
		),

rule core_metrics:
	input:
		tree = phylogeny_out + "/rooted-tree.qza",
		table = diversity_in + "/table.qza",
		repseqs = diversity_in + "/rep-seqs.qza",
		metadata = metadata,
	output:
		diversity_out + "/rarefied_table.qza",
		diversity_out + "/bray_curtis_distance_matrix.qza",
		diversity_out + "/bray_curtis_emperor.qzv",
		diversity_out + "/bray_curtis_pcoa_results.qza",
		diversity_out + "/evenness_vector.qza",
		diversity_out + "/faith_pd_vector.qza",
		diversity_out + "/jaccard_distance_matrix.qza",
		diversity_out + "/jaccard_emperor.qzv",
		diversity_out + "/jaccard_pcoa_results.qza",
		diversity_out + "/observed_features_vector.qza",
		diversity_out + "/shannon_vector.qza",
		diversity_out + "/unweighted_unifrac_distance_matrix.qza",
		diversity_out + "/unweighted_unifrac_emperor.qzv",
		diversity_out + "/unweighted_unifrac_pcoa_results.qza",
		diversity_out + "/weighted_unifrac_distance_matrix.qza",
		diversity_out + "/weighted_unifrac_emperor.qzv",
		diversity_out + "/weighted_unifrac_pcoa_results.qza",
	threads:
		32
	shell:
		"""
		qiime diversity core-metrics-phylogenetic \
			--p-n-jobs-or-threads {threads} \
			--i-phylogeny {input.tree} \
			--i-table {input.table} \
			--p-sampling-depth {sampling_depth} \
			--m-metadata-file {input.metadata} \
			--output-dir {diversity_out}.$$
		mv {diversity_out}.$$/* {diversity_out}/
		rmdir {diversity_out}.$$
		"""

rule alpha:
	input:
		vector = "{dir}/{measure}_vector.qza",
		metadata = metadata,
	output:
		significance = "{dir}/{measure}_significance.qzv",
	threads: 1
	shell:
		"""
		qiime diversity alpha-group-significance \
			--i-alpha-diversity {input.vector} \
			--m-metadata-file {input.metadata} \
			--o-visualization {output.significance}
		"""

rule beta_groups:
	input:
		matrix = diversity_out + "/{method}_distance_matrix.qza",
		metadata = metadata,
	output:
		qzv = diversity_out + "/{method}-by-{col}.qzv"
	threads: 1
	shell:
		"""
		qiime diversity beta-group-significance \
			--i-distance-matrix {input.matrix} \
			--m-metadata-file {input.metadata} \
			--m-metadata-column {wildcards.col} \
			--p-pairwise \
			--o-visualization {output.qzv}
		"""

rule rarefaction:
	input:
		table = diversity_in + "/table.qza",
		tree = phylogeny_out + "/rooted-tree.qza",
		metadata = metadata,
	output:
		qzv = diversity_out + "/alpha-rarefaction.qzv"
	threads: 1
	shell:
		"""
		qiime diversity alpha-rarefaction \
			--i-table {input.table} \
			--i-phylogeny {input.tree} \
			--p-max-depth {sampling_depth} \
			--m-metadata-file {input.metadata} \
			--o-visualization {output.qzv}
		"""

