## rules to export files for R analysis
## - feature table in (QZA and CSV)
## - taxonomy (QZA and CSV)
## - tree in (NWK)
## - metadata in (CSV)
## - representative sequences (FASTA)

export_out = "expdata"

rule export:
	input:
		metadata = export_out + "/sampledata.csv",
		dnastats = export_out + "/dnastats.csv",
		dada2stats = export_out + "/dada2stats.csv",
		table = export_out + "/table.qza",
		table_csv = export_out + "/table.csv",
		taxonomy = export_out + "/taxonomy.qza",
		taxonomy_csv = export_out + "/taxonomy.csv",
		tree = export_out + "/rooted-tree.qza",
		tree_nwk = export_out + "/tree.nwk",
		repseqs = export_out + "/dna-sequences.fasta",
		merged_samplefreq = export_out + "/merged-samplefreq.csv",
		merged_featurefreq = export_out + "/merged-featurefreq.csv",
		filtered_samplefreq = export_out + "/filtered-samplefreq.csv",
		filtered_featurefreq = export_out + "/filtered-featurefreq.csv",


rule export_table:
	input:
		metadata = metadata,
		table = export_in + "/table.qza",
		dnastats = dnastats,
		dada2stats = "merged/dada2stats.csv",
	output:
		metadata_csv = export_out + "/sampledata.csv",
		metadata_tsv = export_out + "/sampledata.tsv",
		table_qza = export_out + "/table.qza",
		table_tsv = export_out + "/table.tsv",
		table_csv = export_out + "/table.csv",
		dnastats = export_out + "/dnastats.csv",
		dada2stats = export_out + "/dada2stats.csv",
	threads:
		1
	shell:
		"""
		cp -p {input} {export_out}/
		cp -p {input.metadata} {output.metadata_tsv}
		grep -v "q2:types" {input.metadata} | \
			tr '\t' ',' | \
			sed "1s/-/./g" \
			> {output.metadata_csv}
		qiime tools export \
			--input-path {input.table} \
			--output-path {export_out}
		biom convert \
			--input-fp {export_out}/feature-table.biom \
			--output-fp {output.table_tsv} \
			--to-tsv
		tail --lines=+2 {output.table_tsv} | \
			tr '\t' ',' | \
			sed "s/#OTU ID/otu/" >{output.table_csv}
		"""

rule export_taxonomy:
	input:
		taxonomy = taxonomy + "/taxonomy.qza",
	output:
		taxonomy_qza = export_out + "/taxonomy.qza",
		taxonomy_tsv = export_out + "/taxonomy.tsv",
		taxonomy_csv = export_out + "/taxonomy.csv",
	threads:
		1
	shell:
		"""
		cp {input} {export_out}/
		qiime tools export \
			--input-path {input.taxonomy} \
			--output-path {export_out}
		( echo "otu,Domain,Phylum,Class,Order,Family,Genus,Species"
		  cut -f 1-2 {output.taxonomy_tsv} | \
		  grep -v "Feature ID" | \
		  sed 's/; */,/g' | \
		  sed 's/Allorhizobium-Neorhizobium-Pararhizobium-Rhizobium/A-N-P-Rhizobium/' | \
		  tr '\t' ',' \
		) >{output.taxonomy_csv}
		"""

rule export_tree:
	input:
		tree = phylogeny_out + "/rooted-tree.qza",
	output:
		tree = export_out + "/rooted-tree.qza",
		tree_nwk = export_out + "/tree.nwk",
	threads:
		1
	shell:
		"""
		cp {input} {export_out}/
		qiime tools export \
			--input-path {input.tree} \
			--output-path {export_out}
		"""

rule export_seqs:
	input:
		repseqs = export_in + "/rep-seqs.qza",
	output:
		repseqs = export_out + "/dna-sequences.fasta",
	threads:
		1
	shell:
		"""
		qiime tools export \
			--input-path {input.repseqs} \
			--output-path {export_out}
		"""

rule export_freq:
	input:
		freq = "{dir}/{name}freq.csv",
	output:
		freq = export_out + "/{dir}-{name}freq.csv",
	shell:
		"""
		cp {input.freq} {output.freq}
		"""


